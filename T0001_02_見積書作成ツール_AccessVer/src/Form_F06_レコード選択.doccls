Option Compare Database
Option Explicit

'**************************
'*レコードの情報選択・取得Form
'**************************

'定数欄
Private Const SOURCE_NAME As String = "F06_レコード選択"

'変数欄
Private previousScreenName As String
Private objRcdSlctForm As clsAbsRcdSlctForm






'******************************************************************************************
'*getter/setter欄
'******************************************************************************************





'******************************************************************************************
'*関数名    ：Form_Open
'*機能      ：
'*引数(1)   ：
'******************************************************************************************
Private Sub Form_Open(Cancel As Integer)
    
    '定数
    Const FUNC_NAME As String = "Form_Open"
    
    '変数
    Dim arrArgs As Variant
    Dim i As Long
    
    On Error GoTo ErrorHandler

    'Argsを取得
    arrArgs = Split(Nz(Me.OpenArgs, ""), ";")
    For i = LBound(arrArgs) To UBound(arrArgs)
        Select Case i
        Case LBound(arrArgs)
            previousScreenName = arrArgs(i)
        Case LBound(arrArgs) + 1
            Set objRcdSlctForm = geneObjRcdSlctForm(CLng(arrArgs(i)))
        Case Else
            Exit For
        End Select
    Next i
    
    'Argsの不正検知
    If previousScreenName = "" Or objRcdSlctForm Is Nothing Then
        #If Not CBool(DEBUG_MODE) Then
            MsgBox "引数が不正です。" & _
                   vbNewLine & _
                   "画面を開けませんでした。", _
                   vbCritical, TOOL_NAME
            Cancel = True
            GoTo ExitHandler
        #Else
            If gObjDB Is Nothing Then Call initializeTool
            previousScreenName = FormName_02
            Set objRcdSlctForm = New clsRcdSlctFormCompany
        #End If
    End If
    
ExitHandler:
    
    Exit Sub
    
ErrorHandler:

    MsgBox "エラーが発生したため、マクロを終了します。" & _
           vbLf & _
           "関数名：" & FUNC_NAME & _
           vbLf & _
           "エラー番号：" & err.Number & vbNewLine & _
           err.description, vbCritical, TOOL_NAME
        
    GoTo ExitHandler
        
End Sub






'******************************************************************************************
'*関数名    ：Form_Load
'*機能      ：
'*引数(1)   ：
'******************************************************************************************
Private Sub Form_Load()
    
    '定数
    Const FUNC_NAME As String = "Form_Load"
    
    '変数
    Dim dicInfo As Object
    
    On Error GoTo ErrorHandler

    Set dicInfo = CreateObject(SCRIPTING_DICTIONARY)
    
    'フォームの情報取得
    If Not objRcdSlctForm.getInfo(dicInfo) Then GoTo ExitHandler
    
    '取得情報をフォームに設定
    Me.Caption = dicInfo("formTitle")
    Me.lbl_announce.Caption = dicInfo("announceTxt")
    Me.lst_selectField.RowSource = dicInfo("rowSourceForList")
    
ExitHandler:

    Exit Sub
    
ErrorHandler:

    MsgBox "エラーが発生したため、マクロを終了します。" & _
           vbLf & _
           "関数名：" & FUNC_NAME & _
           vbLf & _
           "エラー番号：" & err.Number & vbNewLine & _
           err.description, vbCritical, TOOL_NAME
        
    GoTo ExitHandler
        
End Sub




'******************************************************************************************
'*機能      ：btn_ok_Click
'*引数      ：
'******************************************************************************************
Private Sub btn_ok_Click()

    '定数
    Const FUNC_NAME As String = "btn_ok_Click"
    
    '変数
    
    On Error GoTo ErrorHandler

    'click処理
    If Not objRcdSlctForm.btnOKClick(Me.Form) Then GoTo ExitHandler

ExitHandler:

    Exit Sub
    
ErrorHandler:

    MsgBox "エラーが発生したため、マクロを終了します。" & _
           vbLf & _
           "関数名：" & FUNC_NAME & _
           vbLf & _
           "エラー番号：" & err.Number & vbNewLine & _
           err.description, vbCritical, TOOL_NAME
        
    GoTo ExitHandler
        
End Sub



'******************************************************************************************
'*機能      ：btn_cancel_Click
'*引数      ：
'******************************************************************************************
Private Sub btn_cancel_Click()

    '定数
    Const FUNC_NAME As String = "btn_cancel_Click"
    
    '変数
    
    On Error GoTo ErrorHandler
    
    '画面を閉じる
    DoCmd.Close acForm, Me.Name, acSaveNo
    
ExitHandler:

    Exit Sub
    
ErrorHandler:

    MsgBox "エラーが発生したため、マクロを終了します。" & _
           vbLf & _
           "関数名：" & FUNC_NAME & _
           vbLf & _
           "エラー番号：" & err.Number & vbNewLine & _
           err.description, vbCritical, TOOL_NAME
        
    GoTo ExitHandler
        
End Sub
